#+TITLE: Development Configuration for Emacs
#+STARTUP: showall

* Language Neutral Tooling and Utilities

** Git

Use magit for git. The customization on =magit-display-buffer-function= makes it prompts =magit-status= in the same windows instead of opening a new one.

#+begin_src emacs-lisp
  (use-package magit
    ;; Defer loading magit until magit-status is called
    :commands magit-status
    :straight t
    :bind (("C-x g" . magit-status))
    :custom (magit-display-buffer-function
             #'magit-display-buffer-same-window-except-diff-v1))
#+end_src

*** Forge

This package [[https://magit.vc/manual/forge/][forge]] adds integration with Github and Gitlab etc.

*NOTE*: Make sure to configure a GitHub token before using this package!

1. https://magit.vc/manual/forge/Token-Creation.html#Token-Creation
2. https://magit.vc/manual/ghub/Getting-Started.html#Getting-Started

#+begin_src emacs-lisp
  (use-package forge
    :straight t
    :after magit)
#+end_src

** Projectile

This is for project management. It provides utilities to go between different files in the project, and also provides scoping for certain commands. This means that when you run those commands, if they are scopped by projectile, they are only gonna look at files from the project directory.

This [[https://www.reddit.com/r/emacs/comments/azddce/what_workflows_do_you_have_with_projectile_and/][reddit post]] and [[https://www.reddit.com/r/emacs/comments/guzkwo/what_does_projectile_actually_do/][this]] are good readings.

Projectile has a very good [[https://docs.projectile.mx/][documentation]].

#+BEGIN_SRC emacs-lisp
  (with-eval-after-load 'hydra
    (defhydra hydra-projectile (:color blue)
      "
      ^
      ^Projectile^        ^Buffers^           ^Find^              ^Search^
      ^──────────^────────^───────^───────────^────^──────────────^──────^────────────
      _q_ quit            _b_ list            _d_ directory       _r_ replace
      _i_ reset cache     _k_ kill all        _D_ root            _R_ regexp replace
      ^^                  _S_ save all        _f_ file            _s_ search
      ^^                  ^^                  _p_ project         ^^
      ^^                  ^^                  ^^                  ^^
      "
      ("q" nil)
      ("b" counsel-projectile-switch-to-buffer)
      ("d" counsel-projectile-find-dir)
      ("D" projectile-dired)
      ("f" counsel-projectile-find-file)
      ("i" projectile-invalidate-cache :color red)
      ("k" projectile-kill-buffers)
      ("p" counsel-projectile-switch-project)
      ("r" projectile-replace)
      ("R" projectile-replace-regexp)
      ("s" counsel-ag)
      ("S" projectile-save-project-buffers)))

  (use-package projectile
    :straight t
    :diminish project-tile-mode
    :config (projectile-mode)
    :bind (("C-c p" . 'hydra-projectile/body))
    :init
    (when (file-directory-p "~/projects")
      (setq projectile-project-search-path '("~/projects")))
    (setq projectile-switch-project-action #'projectile-dired))

  (use-package counsel-projectile
    :straight t
    :after projectile
    :config (counsel-projectile-mode))
#+END_SRC

** Direnv

This makes Emacs respect =.envrc= file for direnv. I am mainly using =direnv= for automatically starting the =devShell= defined in =flake.nix=, and this makes it so that it brings in the commands enabled by the =devShell=.

#+begin_src emacs-lisp
  (use-package direnv
    :straight t
    :config (direnv-mode))
#+end_src

* Language Server Protocol (LSP)

Thanks to microsoft's great effrot invested in the language backend, we have [[https://microsoft.github.io/language-server-protocol/][LSP]]. I think it is mainly designed for VSCode but many other IDEs have optted in for it. This section configures the integration with LSP for Emacs.

** LSP Mode

#+begin_src emacs-lisp
  (use-package lsp-mode
    :straight t
    :commands (lsp lsp-deferred)  ;; Triggered loading only by those commands
    :init (setq lsp-keymap-prefix "C-c l"))
#+end_src

** Company Mode

Company is a text completion framework for Emacs. It does not have to work with LSP, but working with LSP can provide great experience.

We also add the package =company-box= just to make the prompts a little bit more appealing. This only affects the appearance but not the logic and functionality.

#+begin_src emacs-lisp
  (use-package company
    :straight t
    :hook (prog-mode . company-mode)
    :bind
    (:map company-active-map
          ("<tab>" . company-complete-selection))
    :custom
    (company-minimum-prefix-length 2) ;; Need to type at leasat 2 characters
    (company-idle-delay 0.0))

  (use-package company-box
    :straight t
    :hook (company-mode . company-box-mode))
#+end_src

* C/C++

Use the built-in =cc-mode=, together with =modern-cpp-font-lock= to support C++11, C++14, C++17 and C++20 syntax.

It also add the key bindings and support for =clang-format=.

#+begin_src emacs-lisp
  (defun clang-format-buffer-and-back-to-indentation ()
     "Call clang-format to format the whole buffer, and move the
     cursor to the first non-space character of the current line."
       (interactive)
       (clang-format-buffer)
       (back-to-indentation))

  (use-package clang-format :straight t)

  (use-package cc-mode
    :mode ("\\.h\\'" . c++-mode) ;; Otherwise it will be c-mode
    ;; Hijack the tab key to perform clang-format
    :bind (:map c++-mode-map
                ("<tab>" . clang-format-region)
                ("<C-M-tab>" . clang-format-buffer-and-back-to-indentation)))

  (use-package modern-cpp-font-lock
    :straight t
    :hook (c++-mode . modern-cpp-font-lock-mode))
#+end_src

** CMake

#+begin_src emacs-lisp
  (use-package cmake-mode
    :straight t
    :config (setq cmake-tab-width 4))
#+end_src

* JavaScript, TypeScript, HTML and CSS

Use =web-mode= for this with some customization.

1. By default =web-mode= does not associate typescript and javascript files. Add them to the =:mode= hook.
2. By default =web-mode= globally use indentation =4=. Replace that with =2= to write more compact source code.
3. Enable LSP with =lsp-deferred=. This is currently a *TODO*, because I haven't figure out how to install language server on a per-project basis (or maybe I should have install them globally or just inject to the emacs package).

#+begin_src emacs-lisp
  (use-package web-mode
    :straight t
    :mode (("\\.ts\\'" . web-mode)
           ("\\.tsx\\'" . web-mode)
           ("\\.js\\'" . web-mode)
           ("\\.jsx\\'" . web-mode)
           ("\\.svelte\\'" . web-mode))
    :custom
    (web-mode-markup-indent-offset 2 "Indent = 2 for html")
    (web-mode-css-indent-offset 2 "Indent = 2 for CSS")
    (web-mode-code-indent-offset 2 "Indent = 2 for JS/TS"))
  ;; TODO: make lsp work
  ;; :hook (web-mode . lsp-deferred))
#+end_src

* Nix

#+begin_src emacs-lisp
  (use-package nix-mode :straight t)
#+end_src

* Python

This configures the language server for python. Since for my setup the development environment is all in =flake.nix= so that both =python= and =python-language-server= will not be available until =direnv= kicks in. The current hack is to rely on [[https://www.gnu.org/software/emacs/manual/html_node/elisp/Timers.html][timer]] to delay the initialization of =lsp-mode=, but maybe by applying the proper hook it does not have to be so.

#+begin_src emacs-lisp
  (defun bds/start-lsp-python-ms ()
    ;; The two variable bds/lsp-python-ms-exe and
    ;; bds/lsp-python-ms-python-exe are are to make sure that the python
    ;; and python language server executables are only found once.
    ;;
    ;; This means that the python lsp mode will only be bound to the
    ;; first python and python language server found in one session. I
    ;; personally think this is the most predictable and thus reasonable
    ;; behavior.
    (when (not bds/lsp-python-ms-exe)
      (setq bds/lsp-python-ms-exe (executable-find "python-language-server")))
    (when (not bds/lsp-python-ms-python-exe)
      (setq bds/lsp-python-ms-python-exe (executable-find "python")))

    (when (and bds/lsp-python-ms-python-exe
               bds/lsp-python-ms-exe)
      (setq lsp-python-ms-executable bds/lsp-python-ms-exe)
      (setq lsp-python-ms-python-executable bds/lsp-python-ms-python-exe)
      (require 'lsp-python-ms)
      (lsp)))

  (use-package lsp-python-ms
    :straight t
    :init
    (defvar bds/lsp-python-ms-exe nil)
    (defvar bds/lsp-python-ms-python-exe nil)
    (setq lsp-python-ms-auto-install-server nil) ;; Let me manage it
    :hook (python-mode . (lambda ()
                           ;; Wait for 2 seconds delay before lsp mode
                           ;; kicks in. This is to wait for the direnv
                           ;; to be activated which brings in the python
                           ;; and python language server.
                           (run-at-time "2 sec" nil #'bds/start-lsp-python-ms))))
#+end_src

* Protocol Buffer

#+begin_src emacs-lisp
  (use-package protobuf-mode :straight t)
#+end_src

* Rust
#+begin_src emacs-lisp
  (use-package rust-mode :straight t)
#+end_src

* Common Lisp

#+begin_src emacs-lisp
  (use-package slime
    :straight t
    :config
    (setq inferior-lisp-program "quicklisp run")
    (slime-setup '(slime-fancy)))
#+end_src

* Ledger and Beancount

** Ledger

*TODO*: Add a hydra map for ledger mode.

#+begin_src emacs-lisp
  (use-package ledger-mode :straight t)
#+end_src

** Beancount

#+begin_src emacs-lisp
  (use-package beancount
    :straight (beancount-mode :type git :host github :repo "beancount/beancount-mode"
                              :branch "main")
    :init (add-to-list 'auto-mode-alist '("\\.beancount\\'" . beancount-mode))
    :custom
    (beancount-highlight-transaction-at-point t))
#+end_src

#+begin_src emacs-lisp
  (defun bds/beancount-chapter ()
    (interactive)
    ;; TODO Read this from a template file instead of something like this.
    (let ((this-date (format-time-string "%Y.%m.%d")))
      ;; Chapter Title
      (insert ";; +------------------------------------------------------------+\n")
      (insert (format ";; | %s Account Management                              |\n"
                      this-date))
      (insert ";; +------------------------------------------------------------+\n")
      (insert "\n\n")

      ;; Section Chase
      (insert (format ";; %s Chase\n" this-date))
      (insert (format ";; --------------------\n"))
      (insert "\n\n")

      ;; Section Chase
      (insert (format ";; %s Chase\n" this-date))
      (insert (format ";; --------------------\n"))
      (insert "\n\n")

      ;; Section Rental
      (insert (format ";; %s Rental \n" this-date))
      (insert (format ";; -------------------------\n"))
      (insert "pushtag #rental\n")
      (insert "\n\n")
      (insert "poptag #rental\n")
      (insert "\n\n")))
#+end_src

* Docker

The =dockerfile-mode= makes emacs recognize =Dockerfile= syntax
correctly.

#+begin_src emacs-lisp
  (use-package dockerfile-mode
    :straight t
    :init (add-to-list 'auto-mode-alist '("Dockerfile\\'" . dockerfile-mode)))
#+end_src

* LaTeX

#+BEGIN_SRC emacs-lisp
  (use-package tex :straight auctex)
#+END_SRC

* Graphviz (DOT)

#+begin_src emacs-lisp 
  (use-package graphviz-dot-mode
    :straight t
    :custom (graphviz-dot-indent-width 4))
#+end_src
