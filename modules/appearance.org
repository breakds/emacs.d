:PROPERTIES:
:GPTEL_MODEL: qwen3:30b
:GPTEL_BACKEND: Ollama
:GPTEL_SYSTEM: You are a large language model living in Emacs and a helpful assistant. Respond concisely.
:GPTEL_BOUNDS: nil
:END:
#+TITLE: Configure the Appearance of Emacs
#+STARTUP: showall

* Icons

Use [[https://github.com/domtronn/all-the-icons.el/][all-the-icons]] for all the icon integration with other packages.

#+BEGIN_SRC emacs-lisp
  (use-package all-the-icons
    :straight t
    :config (unless (find-font (font-spec :name "all-the-icons"))
	      (all-the-icons-install-fonts t)))
#+END_SRC

* Theme

Use solarized light as the theme for.

#+BEGIN_SRC emacs-lisp  
  (use-package doom-themes
    :straight t
    :config
    ;; Global Settings
    (setq doom-themes-enable-bold t
    	doom-themes-enable-italic t)
    (load-theme 'doom-monokai-pro t)
    (doom-themes-visual-bell-config)
    ;; Neotree
    ;; Might require nerd-icons
    (doom-themes-neotree-config)
    ;; Treemacs
    ;; Use "doom-colors" for richer icon theme
    (setq doom-themes-treemacs-theme "doom-atom")
    (doom-themes-treemacs-config)
    ;; Org Mode
    (doom-themes-org-config))

  ;; Customization of the loaded theme
  ;;
  ;; Note that you can use `M-x list-faces-display` to see all the faces that are
  ;; active. You can then use `M-x describe-face RET eglot-inlay-hint-face` to
  ;; check the face that interests you
  (with-eval-after-load 'eglot
    (set-face-attribute 'eglot-inlay-hint-face nil
                        :foreground "#ffcce5"
                        :background nil
                        :slant 'italic
                        :height 0.8))
#+END_SRC

Enable [[https://github.com/seagle0128/doom-modeline][doom-modeline]]. It is a nicely done minimalist's modeline. It
requires the icon package [[https://github.com/domtronn/all-the-icons.el/][all-the-icons]] (see above section) to work.

#+BEGIN_SRC emacs-lisp
  (use-package doom-modeline
     :straight t
     :init (doom-modeline-mode 1))
#+END_SRC

* Graphical Window Specific such as X

The =:height= property basically specifies the size of the font. It is
an integer in units of =1/10= point.

#+begin_src emacs-lisp
  (defun bds/apply-appearance-to-frame (frame)
    "Apply GUI-related appearance settings to a frame"
    (with-selected-frame frame
      (when (display-graphic-p frame)
        (modify-frame-parameters frame '((vertical-scroll-bars . nil)))
        (tool-bar-mode -1)
        (tooltip-mode -1)

        ;; ---------- Font ----------
        (let ((hostname (system-name)))
          (set-face-attribute 'default frame
                              :font "JetBrainsMonoNL NFP Light"
                              :height (cond ((member hostname '("brock" "blocker" "hand")) 100)
                                            ((eq window-system 'pgtk) 110)
                                            (t 100))))

        (require 'all-the-icons nil t)
        (require 'nerd-icons nil t))))

  (add-hook 'after-make-frame-functions #'bds/apply-appearance-to-frame)
  (unless (daemonp)
    ;; Non-daemon: apply to the initial frame
    (bds/apply-appearance-to-frame (selected-frame)))
#+end_src

* Time

#+BEGIN_SRC emacs-lisp
  (setq display-time-24hr-format t)
  (display-time)
#+END_SRC

* Details

Always highlight the current line.

#+BEGIN_SRC emacs-lisp
  (global-hl-line-mode t)
#+END_SRC

Always highlight delimiters such as parenthesis.

#+begin_src emacs-lisp
  (use-package rainbow-delimiters
    :straight t
    :hook (prog-mode . rainbow-delimiters-mode))
#+end_src
